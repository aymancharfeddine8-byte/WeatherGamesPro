import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
}

// Read API key from gradle.properties or local.properties
var weatherApiKey: String = (project.findProperty("WEATHER_API_KEY") ?: "").toString()

if (weatherApiKey.isEmpty()) {
    val localPropertiesFile = rootProject.file("local.properties")
    if (localPropertiesFile.exists()) {
        val props = Properties()
        localPropertiesFile.inputStream().use { props.load(it) }
        weatherApiKey = props.getProperty("WEATHER_API_KEY") ?: ""
    }
}

android {
    namespace = "com.example.myapplication"
    compileSdk = 36

    defaultConfig {
        applicationId = "com.example.myapplication"
        minSdk = 26
        targetSdk = 36
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "WEATHER_API_KEY", "\"$weatherApiKey\"")
    }

    androidResources {
        localeFilters += "en"
    }

    buildTypes {
        debug {
            isMinifyEnabled = false
            isTestCoverageEnabled = false
        }
        release {
            isMinifyEnabled = false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
    
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildFeatures {
        buildConfig = true
    }
}

tasks.register("printApiKey") {
    doLast {
        println("DEBUG_API_KEY_START:[$weatherApiKey]:DEBUG_API_KEY_END")
    }
}

dependencies {
    implementation(libs.appcompat)
    implementation(libs.material)
    implementation(libs.activity)
    implementation(libs.constraintlayout)
    implementation(libs.volley)
    implementation(libs.cardview)
    implementation(libs.recyclerview)
    testImplementation(libs.junit)
    androidTestImplementation(libs.ext.junit)
    androidTestImplementation(libs.espresso.core)
}
